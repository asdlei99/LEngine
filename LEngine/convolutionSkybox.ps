////////////////////////////////////////////////////////////////////////////////
// Filename: skybox.ps
////////////////////////////////////////////////////////////////////////////////

TextureCube shaderTexture;
SamplerState SampleType;

cbuffer UpVectorBuffer
{
	float3 g_upVectorVal;
	float g_rightVectorDir;
};

//////////////
// TYPEDEFS //
//////////////
struct PixelInputType
{
    float4 positionSV : SV_POSITION;
    float4 position : POSITION;
};

static const float PI = 3.14159265359f;
////////////////////////////////////////////////////////////////////////////////
// Pixel Shader
////////////////////////////////////////////////////////////////////////////////
float4 ColorPixelShader(PixelInputType input) : SV_TARGET
{
	float3 normal;
	
	[branch] if (g_upVectorVal.z == 1.0f)
	{
		input.position.z = -1.0f;
		input.position.x *= -1;
	}
	[branch] if (g_upVectorVal.z == -1.0f)
	{
		input.position.z = 1.0f;
	}
	[branch] if (g_upVectorVal.x == 1.0f)
	{
		input.position.z = input.position.x;
		input.position.x = -1.0f;
	}
	[branch] if (g_upVectorVal.x == -1.0f)
	{
		input.position.z = -input.position.x;
		input.position.x = 1.0f;
	}
	[branch] if (g_upVectorVal.y == 1.0f)
	{
		input.position.z = input.position.y;
		input.position.y = -1.0f;
	}
	[branch] if (g_upVectorVal.y == -1.0f)
	{
		input.position.z = -input.position.y;
		input.position.y = 1.0f;
	}

	normal = input.position.xyz;

	float3 irradiance = float3(0.0f, 0.0f, 0.0f);
	
	float3 up = float3(0.0f, 1.0f, 0.0f);
	float3 right = cross(up, normal);
	up = cross(normal, right);

	float sampleDelta = 0.025f;
	float nrSamples = 0.0f;
	for (float phi = 0.0f; phi < 2.0 * PI; phi += sampleDelta)
	{
		for (float theta = 0.0f; theta < 0.5 * PI; theta += sampleDelta)
		{
			float3 tangentSample = float3(sin(theta) * cos(phi), sin(theta) * sin(phi), cos(theta));
			float3 sampleVec = (tangentSample.x * right) + (tangentSample.y * up) + (tangentSample.z * normal);

			irradiance += shaderTexture.Sample(SampleType, sampleVec).rgb * cos(theta) * sin(theta);
			nrSamples++;
		}
	}
	irradiance = PI * irradiance * (1.0f / nrSamples);
	return float4(irradiance, 1.0f);
	//return shaderTexture.Sample(SampleType, normal);
	//return float4(float3(normal.y, normal.y, normal.y), 1.0);
	//return float4(1.0, 0.0, 1.0, 1.0);
	//return float4(g_upVectorVal, 1.0f);
}